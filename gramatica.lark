?start: expressao

?expressao: exp_declaracao
          | if_then_else
          | exp_logica
          | exp_arvore

// Precedência Lógica
?exp_logica: exp_logica "or" exp_comparacao  -> bin_or
           | exp_logica "and" exp_comparacao -> bin_and
           | "not" exp_logica                -> un_not
           | exp_comparacao

// Precedência de Comparação
?exp_comparacao: exp_soma "==" exp_soma -> bin_eq
               | exp_soma

// Precedência Aritmética e Concatenação
?exp_soma: exp_soma "+" exp_termo  -> bin_add
         | exp_soma "-" exp_termo  -> bin_sub
         | exp_soma "++" exp_termo -> bin_concat
         | exp_termo

?exp_termo: "-" exp_termo      -> un_neg
          | "length" exp_termo -> un_len
          | aplicacao
          | valor
          | id
          | "(" expressao ")"

// Definições de Árvore
?exp_arvore: "insert" "(" expressao "," expressao ")" -> tree_insert
           | "remove" "(" expressao "," expressao ")" -> tree_remove
           | "search" "(" expressao "," expressao ")" -> tree_search
           | "min" "(" expressao ")"                  -> tree_min
           | "max" "(" expressao ")"                  -> tree_max
           | "inorder" "(" expressao ")"              -> tree_inorder

// Declarações (Let .. In)
exp_declaracao: "let" declaracao_funcional "in" expressao

?declaracao_funcional: dec_variavel
                     | dec_funcao
                     | dec_composta

dec_variavel: "var" id "=" expressao
dec_funcao: "fun" id list_id "=" expressao
dec_composta: declaracao_funcional "," declaracao_funcional

// Aplicação de Função
aplicacao: id "(" list_exp ")"

// Controle de Fluxo
if_then_else: "if" expressao "then" expressao "else" expressao

// Valores
?valor: valor_concreto
?valor_concreto: INT            -> val_int
               | ESCAPED_STRING -> val_string
               | "true"         -> val_bool_true
               | "false"        -> val_bool_false
               | valor_arvore

// Estrutura da Árvore (Construtores explícitos)
?valor_arvore: "Empty" -> val_empty
             | "Node" "(" valor_ordenavel "," expressao "," expressao ")" -> val_node

?valor_ordenavel: INT | ESCAPED_STRING

// Listas auxiliares
list_id: id*
list_exp: (expressao ("," expressao)*)?

// Terminais
id: /[a-zA-Z_][a-zA-Z0-9_]*/
ID: /[a-zA-Z_][a-zA-Z0-9_]*/

%import common.INT
%import common.ESCAPED_STRING
%import common.WS
%ignore WS